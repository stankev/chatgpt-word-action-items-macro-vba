'******************************************************************************************************
'
' Name:                 ActionItemGen
' Description:          This VBA script is intended to be used in Microsoft Word to call the
'                       OpenAI API: https://api.openai.com/v1/chat/completions to generate a formatted
'                       list of action items extracted from meeting minutes, and insert them into
'                       a WORD document.
' Date:                 April 1, 2024
' Author:               Mark Stankevicius
' GitHib Repository:    https://github.com/stankev/chatgpt-word-action-items-macro-vba
'
'******************************************************************************************************
Sub ActionItemGen()
        
    Dim returnedText As String, response As String
    Dim systemPrompt As String, chatPromptMinutes As String
    Dim url As String, requestData As String, modelName As String
    Dim httpRequest As Object
    Dim OpenAI_Api_Key As String
    Dim statusCode As Long
    Dim errorText As String, displayErrorText As String, errorType As String
    Dim startString As String, endString As String
    Dim jsonObjE As Object
    Dim currentDoc As Document
    Dim tempValue As Double, top_pValue As Double, freqPenalty As Double, presPenalty As Double
    
    
    ' Set the info for the POST request
    ' Enter Your API Key in the line below
    OpenAI_Api_Key = "sk-nnnnnnnnnnnnnnnnnnnnnnnnnnnnnnn"   ' MAKE SURE TO REPLACE THE TEXT WITH YOUR API KEY
    
    ' Set the Model Name for use in the request
    modelName = "gpt-4-turbo-preview"

    ' Set the values for temperature, top_p, frequency_penalty, & presence_penalty - you can experiment with these values to get your desired results
    tempValue = 0.2
    top_pValue = 1
    freqPenalty = 0#    ' use a value of 0.0 to direct the model to provide more accuracy and clarity in the generated output to remain true to the source text
    presPenalty = 0#    ' use a value of 0.0 to ensure more accuracy by ensuring that the model maintains fidelity to the source text

    ' Set the API endpoint URL
    url = "https://api.openai.com/v1/chat/completions"  ' compatible with gpt-4, gpt-4-turbo-preview, gpt-3.5-turbo
    
    ' Indicate to the Chat API what action it should take and how to behave
    systemPrompt = "Simulate three brilliant, logical project managers reviewing status meeting minutes and determining the action items. " & _
                "The three project managers review the following status meeting minutes and create their list of action items. The three project managers must " & _
                "carefully review the full list of meeting notes to ensure they capture any action items hidden in the minutes. " & _
                "The three experts then carefully compare their list against the action item list from the other project managers.  " & _
                "Based on their comparison they then generate a final list of action items. Do not generate action items for milestones in the minutes. " & _
                "The action items should list the following for each item: title of the action item, due date if known, dependency if known, and owner of the action item." & _
                "If there is no dependency state None.  Try to infer the due date based on the minutes, but if the due date cannot be determined than specify TBD. " & _
                "Format the output so that it could be inserted into a Word document. Do not bold the generated text. " & _
                "An example format of output is: \nAction Item: Develop Power User training plan and materials.\n   - Due Date: April 25/2024\n   - Dependency: Completion of Teams training materials.\n   - Owner: Alice Williams (Training Lead)\n"


    ' Extract the meeting minutes from the document
    ' Set the delimiters for the search
    startString = "meeting minutes start:"
    endString = "meeting minutes end"
    returnedText = ExtractText(startString, endString)
    If Left(returnedText, 7) = "Error: " Then
        MsgBox returnedText
        Exit Sub
    End If
        
    'Cleaning the mmeeting minutes text and make sure it's all one contunuous string of text without formatting ASCII characters
    chatPromptMinutes = CleanText(returnedText)


    ' Construct JSON request data
    requestData = "{""model"":""" & modelName & """, " & _
              """messages"": [{""role"":""system"",""content"":""" & systemPrompt & """}, " & _
                              "{""role"":""user"",""content"":""" & chatPromptMinutes & """} ], " & _
              """temperature"": " & tempValue & ", " & _
              """top_p"": " & top_pValue & ", " & _
              """frequency_penalty"": " & freqPenalty & ", " & _
              """presence_penalty"": " & presPenalty & ", " & _
              """max_tokens"": 1000}"
    

    ' Create the HTTP request object
    Set httpRequest = CreateObject("MSXML2.XMLHTTP")
       
    ' Set HTTP request parameters
    With httpRequest
       .Open "POST", url, False
       .setRequestHeader "Content-Type", "application/json"
       .setRequestHeader "Authorization", "Bearer " & OpenAI_Api_Key ' Replace with your API key in the code above
       .send requestData
       statusCode = .Status
       response = .responseText
    End With
    

    ' set the location to insert the action items at the end of the document
    Dim existingTextEnd As Long
    Set currentDoc = ActiveDocument                     ' set to the current document
    existingTextEnd = currentDoc.Content.End - 1        ' end of text in document minus 1 so that it doesn't add the text out of range of the document

    
    ' Check if request was successful
    If statusCode = 200 Then
        ' text generation was successful
        Dim displayText As String
        displayText = FormatResponse(response)
        
        'Insert the action items into the Word document at the end of any existing text
        currentDoc.Range(existingTextEnd, existingTextEnd).InsertAfter vbNewLine & displayText
       
    Else
        ' Error condition - Parse the JSON response
        Set jsonObjE = JsonConverter.ParseJson(response)
        errorText = jsonObjE("error")("message")
        errorType = jsonObjE("error")("type")
        
        'Insert error message into Word document
        displayErrorText = "Error: " & vbCrLf & "Error type = " & errorType & vbCrLf & "Error Description = " & errorText
        currentDoc.Range(existingTextEnd, existingTextEnd).InsertAfter vbNewLine & displayErrorText
      
    End If
    
    ' Clean up
    Set httpRequest = Nothing


End Sub

' Extract the generated text from the chat/completions API response
Function FormatResponse(responseText As String) As String
 
    Dim jsonObj As Object
    Dim textToInsert As String
    On Error GoTo FormatResponseError
    
    ' Parse the JSON response
    Set jsonObj = JsonConverter.ParseJson(responseText)
    
    ' Check if JSON parsing was successful
    If jsonObj Is Nothing Then
        ' Handle JSON parsing failure
        FormatResponse = "Error: JSON parsing failed."
        Exit Function
    End If
    
    ' Check if the expected structure of JSON is found
    If Not jsonObj.Exists("choices") Or jsonObj("choices").Count = 0 Then
        ' Handle unexpected JSON structure
        FormatResponse = "Error: Unexpected JSON structure."
        Exit Function
    End If
    
    ' Extract the "content" part from the JSON response - which contains the text of the action items
    textToInsert = jsonObj("choices")(1)("message")("content")
    
    ' Return the extracted text
    FormatResponse = textToInsert
    
    Exit Function
    
FormatResponseError:

    MsgBox "Error " & Err.Number & " (" & Err.Description & ") in procedure FormatResponse."
    
End Function

' Extract the text needed from the word document and return string to the caller
Function ExtractText(startDelim As String, endDelim As String) As String
    On Error GoTo ErrorHandler

    Dim doc As Document
    Dim searchRange As Range
    Dim startPos As Long
    Dim endPos As Long
    Dim extractedText As String
        
    ' Set the document object to current document
    Set doc = ActiveDocument
    
    ' Set the search range to the entire document
    Set searchRange = doc.Range

    With searchRange.Find                       ' work with the current document contents that is copied to searchRange
        .ClearFormatting                        ' clear any previous formatting that might affect the search
        .Text = startDelim                      ' starting delimimeter for the text
        If .Execute Then
            startPos = searchRange.End + 1      ' set the startPos to the end of the found text
        Else
            ' text start delimiter not found
            ExtractText = "Error: """ & startDelim & """ text not found"
            Exit Function
        End If
    End With
            
    Set searchRange = doc.Range                 ' reset the search range to the entire document
            
    With searchRange.Find                       ' work with the current document contents that is copied to searchRange
        .ClearFormatting                        ' clear any previous formatting that might affect the search
        .Text = endDelim                        ' ending delimimeter for the text
        If .Execute Then
            endPos = searchRange.Start - 1      ' move back 1 character before the end delimiter
        Else
            ' text end delimiter not found
            ExtractText = "Error: """ & endDelim & """ text not found"
            Exit Function
        End If
    End With
        
    extractedText = Mid(doc.Range(startPos, endPos).Text, 1)         ' extract the text from the document & return to the caller
    ExtractText = extractedText
    
    Exit Function
    
ErrorHandler:
    ' Handle any unexpected errors
    ExtractText = "Error: an unexpected error occurred in procedure ExtractText"
    
End Function

' Clean up the string of text to remove ASCII formating that will cause issues with the API call
Function CleanText(textToClean As String) As String
    Dim cleanedText As String
    
    cleanedText = Replace(textToClean, vbLf, "")              ' remove line feed characters
    cleanedText = Replace(cleanedText, vbCr, "")              ' remove carriage return characters
    cleanedText = Replace(cleanedText, vbTab, "")             ' remove tab characters
    cleanedText = Replace(cleanedText, vbVerticalTab, "")     ' remove manual line break characters
    cleanedText = Replace(cleanedText, vbCrLf, "")            ' remove combined carriage return and line feed characters
    cleanedText = Replace(cleanedText, Chr(34), Chr(39))      ' replace double quotes with single quote
    CleanText = cleanedText

End Function
